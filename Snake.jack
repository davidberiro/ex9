
class Snake{
        
    field int length, width, radius;
    field int headX;
    field int headY;
    field Array bodyX;
    field Array bodyY;
    field Array foodX, foodY;
    /**
    * constructor for the class
    */
    constructor Snake new(){
        let width =7;
        let radius = 3;
        let length = 4;
        let bodyX = Array.new(40);
        let bodyY = Array.new(40);

        let bodyY[0] = 100;
        let bodyY[1] = 100;
        let bodyY[2] = 100;
        let bodyY[3] = 100;
        let bodyX[0] = 150;
        let bodyX[1] = 153;
        let bodyX[2] = 156;
        let bodyX[3] = 159;

        return this;
    }


    method void drawSnake() {
        var int i;
        let i = 0;

        while (i < length) {
           do Screen.drawCircle(bodyX[i], bodyY[i], 5);
           let i = i+1;
        }


        return;
    }

    method boolean collided() {
        var int i;
        let i = 0;
        let headX = bodyX[length-1];
        let headY = bodyY[length-1];

        if ((headX-radius<0) | (headX+radius > 500) | (headY-radius < 0) | (headY+radius > 245)) {
            return true;
        }

        while (i < (length-1)) {
            if ((headX = bodyX[i]) & (headY = bodyY[i])) {
                return true;
            }

            let i = i+1;
        }

        return false;
    }

    method boolean moveSnake(int direction, int x, int y) {
        var int i;
        let i = 0;

        while (i < (length - 1)) {
            let bodyX[i] = bodyX[i+1];
            let bodyY[i] = bodyY[i+1];
            let i = i+1;
        }

        let i = length - 1;



        if (direction = 0) {
            let bodyX[i] = bodyX[i-1] + width;
        }

        if (direction = 2) {
            let bodyX[i] = bodyX[i-1] - width;
        }

        if (direction = 1) {
            let bodyY[i] = bodyY[i-1] + width;
        }

        if (direction = 3) {
        let bodyY[i] = bodyY[i-1] - width;
        }

        if (collided()) {
            return true;
        }

        do Screen.clearScreen();
        do drawSnake();
        do Screen.drawCircle(x, y, 2);
        do Sys.wait(100);
        return false;
    }

    method int returnX() {
        return bodyX[length-1];
    }

    method int returnY() {
        return bodyY[length-1];
    }


    method void grow(int direction) {
        if (length = 10) {
            do Output.printString("Congratulations! You won!");
            do Sys.halt();
        }

        let length = length + 1;

        if (direction = 1) {
            let bodyX[length-1] = bodyX[length-2];
            let bodyY[length-1] = bodyY[length-2] +width;
        }

        if (direction = 2) {
            let bodyX[length-1] = bodyX[length-2] -width;
            let bodyY[length-1] = bodyY[length-2];
        }

        if (direction = 3) {
            let bodyX[length-1] = bodyX[length-2];
            let bodyY[length-1] = bodyY[length-2] -width;
        }

        if (direction = 0) {
            let bodyX[length-1] = bodyX[length-2] +width;
            let bodyY[length-1] = bodyY[length-2];
        }

        do drawSnake();
        return;
    }

    method void disposeSnake() {
        do bodyX.dispose();
        do bodyY.dispose();
        do Memory.deAlloc(this);
        return;
    }
    
    
}